plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

checkstyle {
    toolVersion = '10.2'
}

javafx {
    version = "17.0.6"
    modules = ['javafx.controls', 'javafx.fxml']
}

sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    String javaFxVersion = '17.0.6'
    String[] platforms = ['win', 'linux', 'mac', 'mac-aarch64']

    for (platform in platforms) {
        implementation group: 'org.openjfx', name: 'javafx-base', version: javaFxVersion, classifier: platform
        implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: platform
        implementation group: 'org.openjfx', name: 'javafx-fxml', version: javaFxVersion, classifier: platform
        implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: platform
    }

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    mainClass.set("bob.Launcher")
}

jar {
    manifest {
        attributes('Main-Class': 'bob.Bob')
    }
}

shadowJar {
    archiveBaseName = "bob"
    archiveClassifier = null
    manifest {
        attributes('Main-Class': 'bob.Launcher')
    }
}

run{
    standardInput = System.in
}
